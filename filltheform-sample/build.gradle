apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.hrs.filltheformsample"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 1
        versionName "0.1"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}

apply plugin: FillTheFormPlugin

//fillTheForm {
//    configDir = 'blah'
//}

task updateFillTheFormData(type: IncrementalReverseTask) {
    configDir = file(project.fillTheForm.configDir)
}

// TODO append on install task

class IncrementalReverseTask extends DefaultTask {
    @InputDirectory
    def File configDir

    @TaskAction
    void execute(IncrementalTaskInputs inputs) {
        //println "test: ${project.android.sdkDirectory}"
        def File outputDir = new File("$project.buildDir${File.separator}FormData")
        // TODO avoid copying files

        if (!inputs.incremental && outputDir.exists())
            project.delete(outputDir.listFiles())

        inputs.outOfDate { change ->
            //def targetFile = new File(outputDir, change.file.name)
            //targetFile.text = change.file.text
            def String adb = "${project.android.sdkDirectory}${File.separator}platform-tools${File.separator}adb";
            // TODO find out target device
            def String[] args = [adb, 'push', change.file, "/sdcard/$change.file.name"]
            def process = new ProcessBuilder(args).redirectErrorStream(false).start()
            int exitCode = process.waitFor()
            if(exitCode != 0) {
                throw new IllegalStateException("Error in adb: " + new BufferedInputStream(process.errorStream).readLines())
            }
        }

        inputs.removed { change ->
            // TODO remove file
            println "removed: ${change.file.name}"
            def targetFile = new File(outputDir, change.file.name)
            targetFile.delete()
        }
    }
}

class FillTheFormPlugin implements Plugin<Project> {
    void apply(Project project) {
        // Add our extension object
        project.extensions.create("fillTheForm", FillTheFormPluginExtension)
    }
}

class FillTheFormPluginExtension {
    def String configDir = 'config'
}

tasks.whenTaskAdded { task ->
    if (task.name.startsWith('install')) {
        //println "HOOK at $task.name"
        task.dependsOn updateFillTheFormData
    }
}